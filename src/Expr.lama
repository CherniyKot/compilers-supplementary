-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Sexp    (string, expr list)  |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  -- printf("%s\n", expr.string);
  case expr of
      Assn(expr1, expr2) -> case evalList(c, {expr1, expr2}) of [[s, w], {v1, v2}] ->
                              case v1 of 
                                [addr, index] ->
                                  addr[index]:=v2;
                                  [[s, w], v2]
                              | v1 -> [[s<-[v1, v2], w], v2]
                              esac
                            esac
    | Set(ident, expr)  ->  case eval(c, expr) of [[s, w], v] ->
                              [[s <- [ident, v], w], 0]
                            esac
    | Seq(expr1, expr2) ->  case eval(c, expr1) of [c, _] ->
                              eval(c, expr2)
                            esac
    | Skip -> [c, 0]
    | If(cond, expr1, expr2) -> 
      case eval(c, cond) of
        [c, 1] -> eval(c, expr1)
      | [c, 0] -> eval(c, expr2)
      esac
    | While(cond, expr) -> eval(c, If(cond, Seq(expr, While(cond, expr)), Skip))
    | DoWhile(expr, cond) -> eval(c, Seq(expr, While(cond, expr)))
    | Var(ident) -> [c, s.lookup(ident)]
    | Ref(ident) -> [c, ident]
    | Const(v) -> [c, v]
    | Binop(op, expr1, expr2) ->  case evalList(c, {expr1, expr2}) of [c, {v1, v2}] ->
                                    [c, evalOp(op, v1, v2)] 
                                  esac
    | Ignore(expr) -> case eval(c, expr) of [c, _] -> [c, 0] esac
    | Scope(defs, expr)  ->
        case foldl (fun (s, def) 
        {
          case def of
            Var(names) -> s.addNames(names)
          | Fun(name, args, expr) -> s.addFunction(name, args, expr)
          esac
        }, s.enterScope, defs) of s ->
          case eval([s, w], expr) of [[s, w], v] ->
            [[s.leaveScope, w], v]
          esac
        esac

    | Call(name, argExprs)  -> 
        case s.lookup(name) of Fun(args, expr) ->
        case evalList(c, argExprs) of [[s, w], vals] ->
        case expr of
          External ->
            case evalBuiltin(name, vals, w) of [v, w] -> [[s, w], v] esac
        | expr ->
          case foldl (fun (s, [arg, v]) 
          {
            s <- [arg, v]
          }, s.enterFunction.addNames(args), zip(args, vals)) of fs ->
                case eval([fs, w], expr) of [[fs, w], v] ->
                  [[s.leaveFunction(fs.getGlobal), w], v]
                esac
          esac
        esac
        esac
        esac
    | String(s) -> [c, s]
    | Array(exprs) -> case evalList(c, exprs) of [[s, w], vals] ->
                        [c, listArray(vals)]
                      esac
    | Sexp(tag, exprs) -> case evalList(c, exprs) of [[s, w], vals] ->
                            [c, listArray(vals)]
                          esac
    | Elem(expr1, expr2) -> case evalList(c, {expr1, expr2}) of [[s, w], {v1, v2}] ->
                              [c, v1[v2]]
                            esac
    | ElemRef(expr1, expr2) ->  case evalList(c, {expr1, expr2}) of [[s, w], {v1, v2}] ->
                                  [c, [v1, v2]]
                                esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
